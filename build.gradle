plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'org.openmicroscopy'
version '5.5.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://artifacts.openmicroscopy.org/artifactory/maven/' }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.testng', name: 'testng', version: '6.14.2'
    testImplementation group: 'jmock', name: 'jmock', version: '1.+'
    testImplementation group: 'com.jamonapi', name: 'jamon', version: '2.81'

    api 'org.openmicroscopy:omero-model:5.5.0-SNAPSHOT'
    implementation 'com.codahale.metrics:metrics-graphite:3.0.2'
    implementation 'com.codahale.metrics:metrics-jvm:3.0.2'
    implementation 'com.codahale.metrics:metrics-logback:3.0.2'
}

test {
    // enable TestNG support (default is JUnit)
    useTestNG() {
        suites "src/test/resources/unit.testng.xml"
    }
}

jar {
    manifest {
        attributes(
                'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Main-Class': 'ome.util.tasks.Run',
                "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' ')
        )
    }
}

// Add publish functionality
apply from: 'publish.gradle'
